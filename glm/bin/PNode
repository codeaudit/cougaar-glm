#!/bin/csh -f

# Environment variables
# ALP_INSTALL_PATH = the head of the alp install.
# ALP3RDPARTY = a directory containing 3rd party jar files
#
# ALP bootstrapping classpath will be:
#  $ALP_DEV_PATH	if defined
#  $ALP_INSTALL_PATH/lib/core.jar
#
# once running, jar files will be searched for in (in order):
#  -Dalp.class.path 	like classpath
#  $CLASSPATH		(alp bootstrapping path from above)
#  $ALP_INSTALL_PATH/lib/*
#  $ALP_INSTALL_PATH/plugins/*
#  -Dalp.system.path=$ALP3RDPARTY
#  $ALP_INSTALL_PATH/sys/*
#

# set ALP_INSTALL_PATH=/alpine/demo98
if (! $?ALP3RDPARTY) then
    setenv ALP3RDPARTY /opt/alp-jars
endif

set LIBPATHS=$ALP_INSTALL_PATH/lib/core.jar
if ($?ALP_DEV_PATH) then
    set LIBPATHS = "${ALP_DEV_PATH}:$LIBPATHS"
endif

# domains are now usually defined by the config file LDMDomains.ini.
# But you may still use properties if you wish.
#set MYDOMAINS="-Dalp.domain.alp=org.cougaar.domain.glm.ALPDomain"
set MYDOMAINS=""
set MYPROPERTIES="$MYDOMAINS -Dalp.install.path=$ALP_INSTALL_PATH -Duser.timezone=GMT"
set MYMEMORY=""

set node = $argv[1]
set rest="$argv[2-]"
if ( "$node" == "" ) then
  set node="Clusters"
endif

if ( "$node" == "admin" ) then
    set args="-c -r -n Administrator -p 8000 $rest"
    set MYMEMORY="-Djava.compiler=NONE"
else 
    set args="-n $node -c $rest"
    # arguments to adjust (defaults are given)
    # -Xmx64m	      # max java heap
    # -Xms3m	      # min (initial) java heap
    # -Xmaxf0.6       # max heap free percent
    # -Xminf0.35      # min heap free percent
    # -Xmaxe4m        # max heap expansion increment
    # -Xmine1m	      # min heap expansion increment
    # -Xoss400k       # per-thread *java* stack size
    set MYMEMORY="-Xmx768m -Xms64m -Xmaxf0.9 -Xminf0.1 -Xmaxe16m -Xoss128k"
    #set MYMEMORY="-Xmx300m -Xms100m"
endif

set osargs=""
set os=`uname`
if ("$os" == "Linux") then
    # only run with green threads, since SMP is still rare
    set osargs="-green"
    # set some system runtime limits
    limit stacksize 16m    #up from 8m
    limit coredumpsize 0   #down from 1g
    #turn this on to enable inprise JIT
    #setenv JAVA_COMPILER javacomp
endif

if ( ! $?JDK_INSTALL_PATH ) then
  echo "JDK_INSTALL_PATH must be set to directory where the JDK is installed"
  exit;
endif

set javaargs="$osargs -Dalp.persistence.enable=true -Xbootclasspath:$ALP_INSTALL_PATH/lib/javaiopatch.jar:$JDK_INSTALL_PATH/jre/lib/rt.jar $MYPROPERTIES $MYMEMORY -classpath $LIBPATHS"

if ($?ALP_DEV_PATH) then
    echo java $javaargs org.cougaar.core.society.Node $args
endif


# exec instead of eval
exec java $javaargs org.cougaar.core.society.Node $args



